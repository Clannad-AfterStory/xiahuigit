FIELD
{
    .1110.离职申请状态;
    .1110.人事部门意见;
    .1110.人事签名;
    .1110.人事签名日期;

    .1257.主管审批状态;
    .1257.人事审批状态;

    .1112.财务结算单照片;

    .1257.name1;
    .1257.name2;
    .1257.name3;
    .1257.name4;
    .1257.UserName;
    .1257.UserName2;
    .1257.UserName2备用;
    .1257.UserName2最终审批;
    .1257.UserName3;
    .1257.UserName4;
    .1257.POST2; 
    .1257.POST3;
    .1257.POST4;
    .1257.1235待处理页;
    .1257.1235待处理页备用;

    .1087.POST;
    .1087.UserName;
    .1087.姓名;
    .1087.GroupID;
    .1087.归属范围;

    .1235.事件状态;
    .1235.处理人姓名;
    .1235.处理人工号;
    .1235.处理人职务;
    .1235.处理人部门;
    .1235.处理日期;
    .1235.审批状态;
    .1235.LinkToFormID;
    .1235.LinkToFormPage;
    .1235.提交人姓名;
    .1235.提交人工号;
    .1235.内容;
    .1235.提交日期;
}
VAR
{
    String status_director, status_personnel, us_current, username3, opinion_personnel;
    String signature_personnel, date_signature, approval_str, post_handle, us_handle, photo_money;
    String date_apply, name_resign, us_resign, date_current, status_approval, name_handle;
    int cp, approval_int, pending_page, np, rec_1087;
    Struct0 st0_dialog1;
    Struct2 st2_1087;
}
CLIENT
{
    FW_FormOpen(1112);
}
SERVER
{
    return(GotoClient);
}
CLIENT
{
    cp = FW_GetCurrentRecNum();
    us_current = FW_GetUserID(2);
    username3 = FW_GetValue(1257, UserName3, cp);
    if(us_current != username3)
    {
        FW_Dialog1(当前登录账号没有审批此流程的权限！);
        return(0);
    }

    status_director = FW_GetValue(1257, 主管审批状态, cp);
    status_personnel = FW_GetValue(1257, 人事审批状态, cp);
    if(status_director != "（已审批）")
    {
        FW_Dialog1(无法审批，暂未到达此流程！);
        return(0);
    }else if(status_personnel == "（已审批）")
    {
        FW_Dialog1(此流程已审批过！);
        return(0);
    }

    opinion_personnel = FW_GetValue(1110, 人事部门意见, cp);
    if(opinion_personnel == "")
    {
        FW_Dialog1(请填写 "人事部门意见"！);
        return(0);
    }

    signature_personnel = FW_GetValue(1110, 人事签名, cp);
    if(signature_personnel == "")
    {
        FW_Dialog1(请填写 "人事签名"!);
        return(0);
    }

    date_signature = FW_GetValue(1110, 人事签名日期, cp);
    if(date_signature == "")
    {
        FW_Dialog1(请填写 "人事签名日期"!);
        return(0);
    }

    photo_money = FW_GetValue(1112, 财务结算单照片, cp);
    if(photo_money == "")
    {
        FW_Dialog1(请上传附件 "财务结算单照片"!);
        return(0);
    }

    st0_dialog1.message = "请填写通过或退回来进行审批。";   //设置可填写的对话框并设置提示信息
    st0_dialog1.sVALUE = "通过";    //预设值为“通过”
    approval_int = FW_Dialog1(st0_dialog1);    //当点击继续才会进行相关审批
    if(approval_int == 1)
    {
        approval_str = st0_dialog1.sVALUE;    //取得审批人输入的内容
        if((approval_str != "通过")&&(approval_str != "退回"))
        {
            FW_Dialog1(只能输入“通过”或“退回”来进行审批！);
            return(0);
        }
    }else
    {
        return(0);
    }
}
SERVER
{
    pending_page = FW_GetValue(1257, 1235待处理页, cp);
    FW_SetValue_w(1235, 事件状态, pending_page, "已处理");
    date_apply = FW_GetValue(1235, 提交日期, pending_page);
    if(approval_str == "通过")
    {
        post_handle = FW_GetValue(1257, POST4, cp);
        st2_1087 = FW_FindRecords("1087, post_handle@POST", 1);
        rec_1087 = st2_1087.RecordNoList.Get[0];
        us_handle = FW_GetValue(1087, UserName, rec_1087);
        name_handle = FW_GetValue(1087, 姓名, rec_1087);
        status_approval = "待" + post_handle +  "审批";

        FW_SetValue_w(1257, name4, cp, name_handle);
        FW_SetValue_w(1257, UserName4, cp, us_handle);
        FW_SetValue_w(1257, 人事审批状态, cp, "（已审批）");
    }else
    {
        post_handle = FW_GetValue(1257, POST2, cp);
        us_handle = FW_GetValue(1257, UserName2最终审批, cp);
        name_handle = FW_GetValue(1257, name2, cp);
        status_approval = "退回待重新审批";

        FW_SetValue_w(1257, 主管审批状态, cp, "（已退回）");
    }
    name_resign = FW_GetValue(1257, name1, cp);
    us_resign = FW_GetValue(1257, UserName, cp);
    date_current = FW_GetSysDate(0);

    np = FW_AddNewPage_w(1235);
    FW_SetValue_w(1257, 1235待处理页, cp, np);

    FW_SetValue_w(1235, 事件状态, np, "待处理");
    FW_SetValue_w(1235, 处理人姓名, np, name_handle);
    FW_SetValue_w(1235, 处理人工号, np, us_handle);
    FW_SetValue_w(1235, 处理人职务, np, post_handle);
    FW_SetValue_w(1235, 处理日期, np, date_current);
    FW_SetValue_w(1235, 审批状态, np, status_approval);
    FW_SetValue_w(1235, LinkToFormID, np, 1110);
    FW_SetValue_w(1235, LinkToFormPage, np, cp);
    FW_SetValue_w(1235, 提交人姓名, np, name_resign);
    FW_SetValue_w(1235, 提交人工号, np, us_resign);
    FW_SetValue_w(1235, 内容, np, "离职申请");
    FW_SetValue_w(1235, 提交日期, np, date_apply);

    FW_SetValue_w(1257, UserName3, cp, us_current);

    FW_SetValue_w(1110, 离职申请状态, cp, status_approval);
    return(GotoClient);
}
CLIENT
{
    FW_SetValue_w(1110, 离职申请状态, cp, status_approval);

    FW_SetValue_w(1257, 1235待处理页, cp, np);
    FW_SetValue_w(1257, UserName3, cp, us_current);
    if(approval_str == "通过")
    {
        FW_SetValue_w(1257, name4, cp, name_handle);
        FW_SetValue_w(1257, UserName4, cp, us_handle);
        FW_SetValue_w(1257, 人事审批状态, cp, "（已审批）");
        FW_Dialog1(审批成功！);
    }else
    {
        FW_SetValue_w(1257, 主管审批状态, cp, "（已退回）");
        FW_Dialog1(已退回此申请！);
    }
}
